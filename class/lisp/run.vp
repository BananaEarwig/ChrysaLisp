(include "lib/asm/func.inc")
(include "sys/mail/class.inc")
(include "sys/task/class.inc")
(include "././fstream/class.inc")
(include "././sstream/class.inc")
(include "./class.inc")
(include "././sym/class.inc")
(include "gui/view/class.inc")
(include "lib/consts/chars.inc")
(include "sys/statics/class.inc")

(def-func 'class/lisp/run :nil 8192)
	;lisp run loop task
	;inputs
	;msg of lisp filename

	(def-vars
		(ptr script stream lisp value args))

	;init app vars
	(push-scope)

	;get param string
	(call 'sys_mail :mymail :nil {script, _})
	(call 'sys_mail :free_obj {script} {script, _, _})

	;string output stream
	(call 'str :create_from_buffer {0, str_gap} {value})
	(call 'sstream :create {value} {stream})

	;create lisp class
	(call 'lisp :create {script, stream, stream, stream} {lisp})
	(call 'obj :deref {stream})
	(vpif {lisp})
		;do we have a main ?
		(call 'view :get_prop {lisp->lisp_environment, static_sym_main} {_, value})
		(breakifnot {value})
		(call 'list :create :nil {args})
		(call 'lisp :repl_apply {lisp, args, value} {_, value})
		(vpif {value->obj_vtable = @class/error/vtable})
			;error in main
			(call 'obj :print {value, lisp->lisp_stderr})
			(call 'stream :write_char {lisp->lisp_stderr, +char_lf})
			(call 'stream :flush {lisp->lisp_stderr})
		(endif)
		(call 'obj :deref {args})
		(call 'obj :deref {value})
		(call 'lisp :deref {lisp})
	(endif)
	(call 'str :deref {script})

	;flush interned symbols, nums and mem blocks
	(fn-bind 'sys/statics/statics :r0)
	(call 'hset :flush '((:r0 statics_sym_intern)))
	(fn-bind 'sys/statics/statics :r0)
	(call 'hset :flush '((:r0 statics_num_intern)))
	(call 'sys_mem :collect)

	(pop-scope)
	(return)

(def-func-end)
